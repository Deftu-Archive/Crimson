buildscript {
    repositories {
        maven {
            url = 'https://maven.minecraftforge.net/'
        }
        maven {
            url = 'https://jitpack.io/'
        }
        maven {
            url = 'https://repo.spongepowered.org/maven/'
        }
    }
    dependencies {
        classpath('com.github.deftu:forgegradle:c00c291546:all')
        classpath('dev.isXander:MixinGradle:0.6-SNAPSHOT')
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'java'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    version = '1.8.9-11.15.1.2318-1.8.9'
    mappings = 'stable_22'

    runDir = 'run'
    clientRunArgs += '--tweakClass xyz.qalcyo.requisite.core.dev.RequisiteDeveloperTweaker'
}

repositories {
    maven {
        name = 'SpongePowered'
        url = 'https://repo.spongepowered.org/maven/'
    }
}

dependencies {
    shade(project(':core')) {
        exclude module: 'Elementa'
        exclude module: 'UniversalCraft'
    }

    shade('gg.essential:elementa-1.8.9-forge:391')

    shade('org.spongepowered:mixin:0.7.11-SNAPSHOT')
    annotationProcessor('org.spongepowered:mixin:0.7.11-SNAPSHOT')
}

mixin {
    disableRefMapWarning = true
    add(sourceSets.main, 'mixins.requisite.refmap.json')
}

sourceSets {
    main {
        ext.refMap = "mixins.requisite.refmap.json"
    }
}

reobfJar {
    archivesBaseName = project_name
    version = project_version
}

task moveResources {
    doLast {
        try {
            ant.move file: "${buildDir}/resources/main", todir: "${buildDir}/classes/java"
        } catch (Exception ignored) {
        }
    }
}

classes.dependsOn(moveResources)
